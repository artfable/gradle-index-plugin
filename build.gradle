group 'com.github.artfable.gradle'
version '0.0.1'

buildscript {
    ext {
        kotlin_version = '1.0.3'
        bintray_version = '1.7.1'
        artifact_plugin_version = '0.0.1'
    }

    repositories {
        jcenter()
        maven {
            url 'http://dl.bintray.com/artfable/gradle-plugins'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_version"
        classpath "com.github.artfable.gradle:gradle-artifact-plugin:$artifact_plugin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'artfable.artifact'

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://github.com/artfable/gradle-index-plugin/master/LICENSE"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "artfable"
            name "Artem Veselov"
            email "art-fable@mail.ru"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'The plugin that allowed to put different index.html file into your build')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.ext.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.ext.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'gradle-plugins'
        name = 'gradle-index-plugin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/artfable/gradle-index-plugin.git'
        version {
            attributes = ['gradle-plugin': 'artfable.index:com.github.artfable.gradle:gradle-index-plugin']
        }
    }
}
